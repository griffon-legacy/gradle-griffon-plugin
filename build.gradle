apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'

ext {
    isSnapshot = version.endsWith('SNAPSHOT')
    poms = [install.repositories.mavenInstaller.pom]
    pomModifications = []
    modifyPom = { Closure modification -> pomModifications << modification }
    deployer = null
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile gradleApi(),
            "org.codehaus.griffon:griffon-launcher:$griffonLauncherVersion"
    groovy "org.codehaus.groovy:groovy-all:$groovyVersion"
}

uploadArchives {
    project.deployer = repositories.mavenDeployer {
        def username = project.properties['codehausUsername'] ?: ''
        def password = project.properties['codehausPassword'] ?: ''

        uploadDescriptor = true
        repository(url: codehausReleaseUrl) {
            authentication(userName: username, password: password)
        }
        snapshotRepository(url: codehausSnapshotUrl) {
            uniqueVersion = true
            authentication(userName: username, password: password)
        }
    }
    poms << deployer.pom
}

poms*.whenConfigured { pom ->
    pomModifications.each { modification ->
        pom.project {
            modification.delegate = delegate
            modification.resolveStrategy = Closure.DELEGATE_FIRST
            modification()
        }
    }
}

modifyPom {
    name 'Griffon Gradle plugin'
    description project.description
    url 'http://griffon.codehaus.org'
    inceptionYear '2011'
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    scm {
        url 'https://github.com/griffon/gradle-griffon-plugin/'
    }
    developers {
        [aalmiray: 'Andres Almiray'].each { devId, devName ->
            developer {
                id devId
                name devName
                roles {
                    role 'Developer'
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.3'
    jarFile = 'gradle/wrapper/gradle-wrapper.jar'
}
